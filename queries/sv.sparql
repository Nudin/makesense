SELECT ?lexeme ?item ?lemma ?desc ?cat ?genus (wd:Q9027 as ?lang) WITH {
SELECT * WHERE {
  # The order of these joins is manually optimized
  hint:Query hint:optimizer "None".

  # Search for lexemes in any language except English and German
  # (they have their own query)
  ?lexeme  dct:language wd:Q9027.
  ?lexeme wikibase:lemma ?lemma.
  # Search for potentially matching items with descriptions
  ?item rdfs:label ?lemma.
  ?item schema:description ?desc.	FILTER(LANG(?desc) = LANG(?lemma)).
  # get lexical category and genus
  ?lexeme         wikibase:lexicalCategory ?cat.
  OPTIONAL { ?lexeme wdt:P5185 ?genus. } .

  # Remove matches that are already connected
  FILTER NOT EXISTS {
    ?lexeme ontolex:sense ?sense.
    ?sense wdt:P5137 ?item.
  }.
}} AS %i
WHERE {
  include %i
  # Remove various common false positives
  FILTER NOT EXISTS {
    VALUES ?not {
     wd:Q101352   # family name
     wd:Q11424    # movie
     wd:Q11446    # ships
     wd:Q11668273 # shogi pieces
     wd:Q1229071  # motif
     wd:Q13417114 # noble family
     wd:Q1391494  # technical terminology
     wd:Q147276   # names of any kind
     wd:Q15632617 # fictional person
     wd:Q15642541 # human settlement definitions
     wd:Q17155032 # type of software
     wd:Q1725664  # terminology of any kind
     wd:Q17537576 # any creative work incl. music, video, text, etc.
     wd:Q17638537 # routine - fix computer terminology
     wd:Q19798644 # wikibase model terms
     wd:Q19798645 # wikibase datatype
     wd:Q20202269 # music terms
     wd:Q208569   # album
     wd:Q21191270 # tv series episode
     wd:Q215380   # bands
     wd:Q2235308  # types of ships
     wd:Q23786    # eye color
     wd:Q3305213  # painting
     wd:Q3491429  # military terms
     wd:Q395      # math
     wd:Q40056    # software
     wd:Q4167836  # wikimedia cat
     wd:Q43229    # organization of any kind
     wd:Q482994   # album
     wd:Q494452   # heraldry
     wd:Q5        # human
     wd:Q5185279  # poem
     wd:Q56876983 # symbol (heraldic)
     wd:Q57814795 # domesticated animal
     wd:Q7187     # gene
     wd:Q7366     # song
     wd:Q7397     # software
     wd:Q8134     # economics
     
     # improvements for swedish
     wd:Q3744866  # common charge (heraldic)
     wd:Q1145276  # fictional country (star trek)
     wd:Q99281788 # star trek location
     wd:Q13406463 # wikimedia list article
     wd:Q19798642 # WD value
     # stuff from riksdagen
     wd:Q99045339     # written question
     wd:Q1505023      # interpellation
     wd:Q10429085     # report
     # 2021-07-28
     wd:Q15831596  # class of fictional entities e.g. star wars sandcrawler
     wd:Q105543609 # musical work
     wd:Q3331189   # version, edition, or translation
     wd:Q1897960   # trotting horse
     wd:Q15056993  # aircraft family
     wd:Q355304    # watercourse (this has a lot of false positives that are not relevant in a Swedish lexeme context)
     wd:Q819989    # lunar crater
     wd:Q58408484  # Wikimedia list of persons by surname
     wd:Q386724    # work
    }.
    ?item wdt:P31 ?not
  }
  
  # Remove various common false positives via P279
  FILTER NOT EXISTS {
    VALUES ?not {
      wd:Q55043        # gymnasium
      wd:Q15284        # municipality
      wd:Q15911314     # association
      # 2021-07-29
      wd:Q13220204     # second-level administrative country subdivision
    }.
    ?item wdt:P279 ?not.
  }   
  FILTER NOT EXISTS { ?item a wikibase:Property}.     # filter properties
  FILTER NOT EXISTS { ?item wdt:P131 ?_ }.            # filter geographic items
  FILTER NOT EXISTS { ?lexeme wdt:P5402 ?_ }.         # filter homographs
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q494452  }.   # blazonry
  FILTER NOT EXISTS { ?item wdt:P279* wd:Q494452  }.           # blazonry
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q1406161  }.  # artistic theme 
  FILTER NOT EXISTS { ?item wdt:P361 wd:Q18336}.               # part of heraldry
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q11446  }.    # ship 
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q452237  }.   # motion
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q7302866  }.   # audio track (basshunter song)
  # 2021-07-28
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q14897293  }. # fictional entity
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q23010327  }. # fictional objects
  FILTER NOT EXISTS { ?item wdt:P136 [].}                      # genre (music, etc)
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q4167410  }.  # disambiguation pages
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q17537576  }. # creative work         (very expensive)
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q35872  }.    # boat                  (medium expensive)
}
